/*
  rndseq.dlg

*! VERSION 1.0.5  30sep2019

*/

VERSION 12.0

INCLUDE _std_medium
DEFINE _dlght 400
INCLUDE header
HELP hlp1, view("help rndseq")
RESET res1

DIALOG main, label("rndseq - Generation of Random Sequences") tabtitle("Main")
BEGIN
	TEXT  		tx_type	_lft  	_top 	120 	., label("Randomization type:")
	RADIO		rb_simp	+125	@		80		., label("Simple") first default(1) /*
										*/			onclickon(program type_simple) option(simple)
	RADIO		rb_bloc	+90		@		85		., label("Block") middle /*
										*/			onclickon(program type_block) option(block)
	RADIO		rb_efr	+95		@		85		., label("Efron") last /*
										*/			onclickon(program type_efron) option(efron)

	EDIT		ed_drug	_lft	_ls	 125	., option(drug) label("Drug")
	TEXT  		tx_drug	+135 	@	 100 	., label("Drug")
	EDIT		ed_prot	_lft	_ls	 125	., option(prot) label("Protocol")
	TEXT  		tx_prot	+135   	@	 100 	., label("Protocol")
	TEXT  		tx_tre	_lft  	_ms	 100 	., label("List of Treatments:")
	EDIT		ed_tre	_lft	_ss	 400	., option(treat) label("List of Treatments")
	TEXT  		tx_note1 _lft	_ss	 400 	., label("Comma-separated, for example: Treat A, Treat B, Treat C")
	EDIT		ed_n	_lft	_ls  125	., option(n) numonly label("Number of subjects")
	TEXT  		tx_n	+135  	@ 	 200 	., label("Number of subjects")
	EDIT		ed_rat1	_lft	195	 125	., option(ratio) label("List of randomization ratios")
	TEXT  		tx_rat1	+135   	@	 260 	., label("List of randomization ratios; for example: 1 2 2")
	EDIT		ed_bs	_lft	195	 125	., option(bs) numonly label("Block size")
	TEXT  		tx_bs	+135   	@	 200 	., label("Block size")
	EDIT		ed_lp	_lft	195	 125	., option(lp) numonly label("Low probability")
	TEXT  		tx_lp	+135   	@	 260 	., label("Low probability")
	EDIT		ed_rat2	_lft	225	 125	., option(ratio) label("List of randomization ratios")
	TEXT  		tx_rat2	+135   	@	 260 	., label("List of randomization ratios; for example: 1 2 2")
	EDIT		ed_hp	_lft	225	 125	., option(hp) numonly label("High probability")
	TEXT  		tx_hp	+135   	@	 260 	., label("High probability")
		
	TEXT 		tx_usin	_lft	_xls	 400	., label("Stata Data file to store results (working directory is used by default):")
	FILE		fl_usin	_lft	_ss	 400	., label("Browse...") default("rndseq_results.dta")/*
					*/							dialogtitle("Save Rndseq Results File") /*
					*/							filter("Stata Data (*.dta)|*.dta") save option(using)
	CHECKBOX	cb_repl	_lft	_ms	 250  	., label("Replace dataset if it already exists") default(1)
	
	GROUPBOX	gb_nst	_lft    _ls	 400  	55, label("Name of the study")
	EDIT		ed_nst	_ilft   _ss	 380	., option(nst)
END

DIALOG opt, tabtitle("Options")
BEGIN
	GROUPBOX	gb_cent	_lft	_top 400	100, label("Labels of Centers")
	EDIT		ed_cent	_ilft	_ms	 380	., option(center) label("Labels of Centers")
	TEXT  		tx_not1	_ilft  	_ss	 380 	., label("Comma-separated, for example: Center 1, Center 2")
	EDIT		ed_pc 	_ilft	_ms	 125	., option(pcenter) label("Expected proportions")
	TEXT  		tx_pc  	+135  	@	 245 	., label("Expected proportions; default: Equal prop.")
	GROUPBOX	gb_str	_lft    _xls 400  	100, label("Labels of Strata for Centers")
	EDIT		ed_str	_ilft	_ms	 380	., option(strata) label("Labels of Strata for Centers")
	TEXT  		tx_str 	_ilft   _ss	 380 	., label("Comma-separated, for example: Stratum A, Stratum B, Stratum C")
	EDIT		ed_sc 	_ilft	_ms	 125	., option(pstrata) label("Expected proportions")
	TEXT  		tx_sc  	+135  	@	 245 	., label("Expected proportions; default: Equal prop.")
	EDIT		ed_pre	_lft	_xls 125	., option(prefix) label("List of prefixes")
	TEXT  		tx_pre	+135    @ 	 260 	., label("List of prefixes; for example: 1A 1B 1C 2A 2B 2C")
	EDIT		ed_seed	_lft	_ms	 125	., option(seed) numonly label("Seed")
	TEXT  		tx_seed	+135    @  	 260 	., label("Seed")
	CHECKBOX	cb_nop	_lft	_ms	 250  	., label("Do not print the results") default(0)
END


PROGRAM type_simple
BEGIN
	call main.ed_rat1.show
	call main.tx_rat1.show	
	call main.ed_bs.hide
	call main.tx_bs.hide
	call main.ed_lp.hide
	call main.tx_lp.hide
	call main.ed_rat2.hide
	call main.tx_rat2.hide
	call main.ed_hp.hide
	call main.tx_hp.hide
END

PROGRAM type_block
BEGIN
	call main.ed_bs.show
	call main.tx_bs.show
	call main.ed_rat2.show
	call main.tx_rat2.show
	call main.ed_rat1.hide
	call main.tx_rat1.hide	
	call main.ed_lp.hide
	call main.tx_lp.hide
	call main.ed_hp.hide
	call main.tx_hp.hide
	call main.ed_hp.hide
	call main.tx_hp.hide
END

PROGRAM type_efron
BEGIN
	call main.ed_lp.show
	call main.tx_lp.show
	call main.ed_hp.show
	call main.tx_hp.show
	call main.ed_bs.hide
	call main.tx_bs.hide
	call main.ed_rat1.hide
	call main.tx_rat1.hide	
	call main.ed_rat2.hide
	call main.tx_rat2.hide
END


PROGRAM command
BEGIN
	put "rndseq " 
	option radio(main rb_simp rb_bloc rb_efr)
	beginoptions
		require main.ed_drug main.ed_prot main.ed_tre main.ed_n
		optionarg main.ed_drug main.ed_prot main.ed_tre main.ed_n

		if main.rb_simp {
			optionarg main.ed_rat1
		}
		if main.rb_bloc {
			require main.ed_bs
			optionarg main.ed_bs main.ed_rat2
		}
		if main.rb_efr {
			require main.ed_lp main.ed_hp
			optionarg main.ed_lp main.ed_hp
		}

		if !main.fl_usin.isdefault() {
			optionarg main.fl_usin
		}
		if !main.cb_repl {
			put " noreplace"
		}

		optionarg opt.ed_cent opt.ed_pc opt.ed_str opt.ed_sc opt.ed_pre opt.ed_seed
		if opt.cb_nop {
			put " noprint"
		}

		optionarg main.ed_nst
	endoptions
END
